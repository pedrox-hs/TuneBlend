import org.gradle.api.internal.FactoryNamedDomainObjectContainer

allprojects {
    if (!file('build.gradle.kts').exists()) return

    apply plugin: libs.plugins.sonarCloud.get().pluginId

    sonar {
        properties {
            def isAndroidProject = project.extensions.findByName('android') != null

            property('sonar.projectName', project.path == ':' ? project.name : project.path.substring(1))

            properties["sonar.sources"] = properties["sonar.sources"] ?: []
            properties["sonar.sources"] += isAndroidProject ? project.android.sourceSets.srcDirs() : []
            properties["sonar.tests"] = properties["sonar.tests"] ?: []
            properties["sonar.tests"] += isAndroidProject ? project.android.sourceSets.testDirs() : []

            property('sonar.androidLint.reportPaths', fileTree(
                    dir: layout.buildDirectory,
                    include: '/reports/lint-results*.xml',
            ).files)
            property('sonar.kotlin.detekt.reportPaths', fileTree(
                    dir: layout.buildDirectory,
                    include: '/reports/detekt/**/*.xml',
            ).files)

            property('sonar.coverage.jacoco.xmlReportPaths', fileTree(
                    dir: layout.buildDirectory,
                    include: [
                            '/reports/coverage/**/report.xml',
                            '/reports/jacoco/**/*.xml',
                    ],
            ).files)
            property('sonar.coverageReportPaths', fileTree(
                    dir: layout.buildDirectory,
                    include: [
                            '/reports/coverage/**/report.xml',
                            '/reports/jacoco/**/*.xml',
                    ],
            ).files)

            property('sonar.testExecutionReportPaths', fileTree(
                    dir: layout.buildDirectory,
                    include: [
                            '/outputs/unit_test_code_coverage/**/*.exec',
                            '/outputs/code_coverage/**/*.ec',
                    ],
            ).files)
        }
    }
}

tasks.matching {
    task -> task.name.startsWith('sonar')
}.each { task ->
    task.dependsOn provider {
        subprojects.collect { subproject ->
            subproject.tasks.matching {
                it.name ==~ /(lint|detekt|jacocoTestReport|createDebugUnitTestCoverageReport|build)/
            }
        }
    }
}

FactoryNamedDomainObjectContainer.metaClass.srcDirs = {
    [
            delegate.main.kotlin.srcDirs,
            delegate.debug.kotlin.srcDirs,
            delegate.release.kotlin.srcDirs,
    ].onlyExisting()
}

FactoryNamedDomainObjectContainer.metaClass.testDirs = {
    [
            delegate.test.kotlin.srcDirs,
            delegate.testDebug.kotlin.srcDirs,
            delegate.androidTest.kotlin.srcDirs,
    ].onlyExisting()
}

List.metaClass.onlyExisting = {
    delegate.collectMany { it.asList() }
            .toSet()
            .findAll { it.exists() }
}
