import org.gradle.api.internal.FactoryNamedDomainObjectContainer

def projectKey = findProperty('sonar.projectKey') ?: System.getenv('SONAR_PROJECT_KEY')
def organization = findProperty('sonar.organization') ?: System.getenv('SONAR_ORGANIZATION')
def hostUrl = findProperty('sonar.host.url') ?: System.getenv('SONAR_HOST_URL')

allprojects {
    apply plugin: libs.plugins.sonarCloud.get().pluginId

    afterEvaluate {
        project.configureSonarProperties()
    }
}

tasks.matching {
    task -> task.name.startsWith('sonar')
}.each { task ->
    task.dependsOn provider {
        subprojects.collect { subproject ->
            subproject.tasks.matching {
                it.name ==~ /(lint|detekt|jacocoTestReport|createDebugUnitTestCoverageReport|build)/
            }
        }
    }
}

Project.metaClass.configureSonarProperties = {
    def project = delegate

    project.sonar {
        properties {
            property('sonar.projectKey', projectKey)
            property('sonar.projectName', project.path == ':' ? rootProject.name : project.path.substring(1))

            property('sonar.organization', organization)
            property('sonar.host.url', hostUrl ?: 'https://sonarcloud.io')

            properties["sonar.sources"] += project.android?.sourceSets?.srcDirs() ?: []
            properties["sonar.tests"] = properties["sonar.tests"] ?: []
            properties["sonar.tests"] += project.android?.sourceSets?.testDirs() ?: []

            property('sonar.androidLint.reportPaths', "${layout.buildDirectory}/reports/lint-results*.xml")
            property('sonar.kotlin.detekt.reportPaths', "${layout.buildDirectory}/reports/detekt/**/*.xml")
            property('sonar.coverage.jacoco.xmlReportPaths', "${layout.buildDirectory}/reports/jacoco/**/*.xml",)
            property('sonar.coverageReportPaths', "${layout.buildDirectory}/reports/coverage/**/report.xml")

            property('sonar.testExecutionReportPaths', [
                    "${layout.buildDirectory}/outputs/unit_test_code_coverage/**/*.exec",
                    "${layout.buildDirectory}/outputs/code_coverage/**/*.ec",
            ])
        }
    }
}

FactoryNamedDomainObjectContainer.metaClass.srcDirs = {
    [
            delegate.main.kotlin.srcDirs,
            delegate.debug.kotlin.srcDirs,
            delegate.release.kotlin.srcDirs,
    ].onlyExisting()
}

FactoryNamedDomainObjectContainer.metaClass.testDirs = {
    [
            delegate.test.kotlin.srcDirs,
            delegate.androidTest.kotlin.srcDirs,
    ].onlyExisting()
}

List.metaClass.onlyExisting = {
    delegate.collectMany { it.asList() }
            .toSet()
            .findAll { it.exists() }
}
